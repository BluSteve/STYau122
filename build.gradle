import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.1.1'
    }
}

plugins {
    id 'java'
    id "me.champeau.jmh" version "0.6.6"
}

group 'edu.nushigh'
version ''

repositories {
    mavenCentral()
}

String outputJar = "${buildDir}/libs/${rootProject.name}.jar"
String dependsDir = "${buildDir}/libs/dependencies/"

jar {
    clean
    manifest {
        attributes(
                'Main-Class': 'examples.ForJinherng',
                'Multi-Release': 'true'
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

jmh {
    resultFormat = 'CSV'
}

task myProguardTask(type: ProGuardTask) {
    dependsOn 'jar'

    task copyDepends(type: Copy) {
        from configurations.default
        into "${dependsDir}"
    }
    dependsOn 'copyDepends'

    injars "${outputJar}"

    libraryjars "${System.getProperty('java.home')}/jmods"
    libraryjars "${dependsDir}"

    dontwarn "org.apache.logging.log4j.**"
    keepattributes "Signature"
    keep "class org.apache.logging.log4j.** { *; }"
    keep "class org.apache.commons.csv.** { *; }"

    keep "class * extends com.google.gson.TypeAdapter"
    keep "class * implements com.google.gson.TypeAdapterFactory"
    keep "class * implements com.google.gson.JsonSerializer"
    keep "class * implements com.google.gson.JsonDeserializer"

    keep "public class runcycle.structs.** {*;}"
    keep "public class nddo.structs.** {*;}"
    keep "public class frontend.** {*;}"

    dontobfuscate()
    optimizations("!library/gson")

    dontwarn "org.ejml.**"
    dontwarn "pabeles.**"

    keep "public class frontend.Main { public static void main(java.lang.String[]);}"

    outjars "${buildDir}/libs/proguard.jar"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation files('libs/ejml-v0.42-SNAPSHOT.jar') // todo add docs
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-csv:1.9.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.0'

    jmh 'org.openjdk.jmh:jmh-core:1.34'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.34'
}
